import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'idea'
    id 'java'
    id "org.jetbrains.intellij" version "0.4.21"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

repositories {
    mavenCentral()
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    version ideaVersion
    pluginName 'PHPUnit Helper'
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            'CSS',
            'java-i18n',
            'properties',
            'yaml',
            'JavaScriptLanguage',
            "java"
    ]
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

patchPluginXml {
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

group 'com.dimabdc.idea.php.phpunit'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
